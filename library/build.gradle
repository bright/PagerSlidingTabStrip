plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}


//apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

bintray {
    user = 'bright'
    key = System.getenv('BINTRAY_KEY')
    publications = ['bintray']
    pkg {
        repo = 'maven'
        desc = project.POM_NAME
        name = project.POM_ARTIFACT_ID
        userOrg = 'bright'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bright/PagerSlidingTabStrip'
        version {
            name = project.VERSION_NAME
            desc = "${project.POM_NAME} ${project.VERSION_NAME}"
            vcsTag = "v${project.VERSION_NAME}"
        }
    }
}

publishing {
    publications {
        bintray(MavenPublication) {
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/library-release.aar")
            artifactId project.POM_ARTIFACT_ID
            groupId project.GROUP
            version project.VERSION_NAME
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}